apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'jacoco'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

// --- JAVADOC ---

javadoc {
	options.encoding = 'UTF-8'
	failOnError = false
}

// --- JACOCO ---

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    } 
}

// --- CONFIGURATIONS ---

configurations {
    runtime
    ecj
}

configurations.all {
    // resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// --- REPOSITORIES ---

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

// --- SOURCE ---

sourceSets {
	main {
		java {
			srcDirs = ["src/main/java"]
		}
		resources {
		    srcDirs = ["src/main/java"]
		    includes = ["**/*.ico"]
		}
	}
	test {
	    resources {
	        srcDirs = ["src/test/java"]
	    }
	}
}

// --- DEPENDENCIES ---

dependencies {
	
    // ============== COMPILATION ==============
    
    testCompile 'junit:junit:4.12'
	ecj 'org.eclipse.jdt.core.compiler:ecj:4.4.2'

	// ================ TESTING ================

	// https://mvnrepository.com/artifact/com.openpojo/openpojo
	testCompile group: 'com.openpojo', name: 'openpojo', version: '0.8.10'	
	
    // =============== MOLECULER ===============

	// Moleculer core (required)
	compile group: 'com.github.berkesa', name: 'moleculer-java', version: '1.1.0'
	
	// Moleculer developer console (optional)
    compile group: 'com.github.berkesa', name: 'moleculer-java-repl', version: '1.0.5'
    
    // ================ LOGGING ================
	
	// http://mvnrepository.com/artifact/org.slf4j/slf4j-api
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
       
	// http://mvnrepository.com/artifact/org.slf4j/slf4j-jdk14
	compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.26'

	// https://mvnrepository.com/artifact/org.slf4j/log4j-over-slf4j
	compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.26'

	// https://mvnrepository.com/artifact/org.slf4j/jcl-over-slf4j
	compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.26'

	// =============== CDI FRAMEWORKS ===============
	 
    // --- SPRING DEPENDENCY INJECTION FRAMEWORK ---

	// https://mvnrepository.com/artifact/org.springframework/spring-context
	compile group: 'org.springframework', name: 'spring-context', version: '5.1.8.RELEASE'
	
	// ============ WEB CONNECTORS =============
		
	// --- NETTY SERVER ---
	
	// https://mvnrepository.com/artifact/io.netty/netty-handler
	compile group: 'io.netty', name: 'netty-handler', version: '4.1.36.Final'

	// https://mvnrepository.com/artifact/io.netty/netty-codec-http
	compile group: 'io.netty', name: 'netty-codec-http', version: '4.1.36.Final'

	// --- SERVLET API ---

	// https://mvnrepository.com/artifact/javax.servlet/javax.servlet-api
	compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

	// --- WEBSOCKET API (FOR SERVLETS) ---

	// https://mvnrepository.com/artifact/org.atmosphere/atmosphere-runtime
	compile group: 'org.atmosphere', name: 'atmosphere-runtime', version: '2.5.3'

	// --- WEBSOCKET API (CLIENT FOR TESTING) ---
		
	// https://mvnrepository.com/artifact/org.java-websocket/Java-WebSocket
	compile group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.4.0'
	
	// ========== ASYNC HTTP CLIENT ============
	
	// https://mvnrepository.com/artifact/org.apache.httpcomponents/httpasyncclient
	compile group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.4'

	// https://mvnrepository.com/artifact/org.apache.httpcomponents/httpmime
	compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.9'

	// ============ JETTY SERVER ===============
		
	// https://mvnrepository.com/artifact/org.eclipse.jetty.websocket/javax-websocket-server-impl
	compile group: 'org.eclipse.jetty.websocket', name: 'javax-websocket-server-impl', version: '9.4.19.v20190610'
	
	// https://mvnrepository.com/artifact/org.eclipse.jetty/jetty-continuation
	compile group: 'org.eclipse.jetty', name: 'jetty-continuation', version: '9.4.19.v20190610'
	
	// ========== MULTIPART PARSER =============
	
	// https://mvnrepository.com/artifact/org.synchronoss.cloud/nio-multipart-parser
	compile group: 'org.synchronoss.cloud', name: 'nio-multipart-parser', version: '1.1.0'

	// ========== TEMPLATE ENGINES =============

	// --- DATATREE TEMPLATES ---

	// https://mvnrepository.com/artifact/com.github.berkesa/datatree-templates
	compile group: 'com.github.berkesa', name: 'datatree-templates', version: '1.1.1'
	
	// --- FREEMARKER ---
	
	// https://mvnrepository.com/artifact/org.freemarker/freemarker
	compile group: 'org.freemarker', name: 'freemarker', version: '2.3.28'
	
	// --- JADE ---
	
	// https://mvnrepository.com/artifact/de.neuland-bfi/jade4j
	compile group: 'de.neuland-bfi', name: 'jade4j', version: '1.2.7'

	// --- MUSTACHE ---
		
	// https://mvnrepository.com/artifact/com.github.spullara.mustache.java/compiler
	compile group: 'com.github.spullara.mustache.java', name: 'compiler', version: '0.9.6'

	// --- THYMELEAF ---
	
	// https://mvnrepository.com/artifact/org.thymeleaf/thymeleaf
	compile group: 'org.thymeleaf', name: 'thymeleaf', version: '3.0.11.RELEASE'

	// --- PEBBLE ---
	
	// https://mvnrepository.com/artifact/com.mitchellbosecke/pebble
	compile group: 'com.mitchellbosecke', name: 'pebble', version: '2.4.0'

}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.github.berkesa'

version = '1.0.1'

modifyPom {
    project {
        artifactId 'moleculer-java-web'
        name 'Java API gateway service for Moleculer'
        description 'Non-blocking J2EE and Netty connector with WebSocket and SSL support.'
        url 'https://moleculer-java.github.io/moleculer-java-web/'
        inceptionYear '2019'

        scm {
            url 'https://moleculer-java.github.io/moleculer-java-web/'
            connection 'scm:https://github.com/moleculer-java/moleculer-java-web.git'
            developerConnection 'scm:git://github.com/moleculer-java/moleculer-java-web.git'
        }

        licenses {
            license {
                name "The MIT License"
                url "http://www.opensource.org/licenses/MIT"
                distribution 'repo'
            }
        }
        
        dependencies {
            dependency {
                groupId 'com.github.berkesa'
                artifactId 'moleculer-java'
                version '1.1.0'
                scope 'runtime'
            }
        }

        developers {
            developer {
                id 'berkesa'
                name 'Andras Berkes'
                email 'andras.berkes@programmer.net'
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
	snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

// --- COMPILATION ---

compileJava {
	options.fork = true
	options.forkOptions.with {
		executable = 'java'
		jvmArgs = ['-classpath', project.configurations.ecj.asPath, 'org.eclipse.jdt.internal.compiler.batch.Main', '-nowarn']
	}
}

// --- JAR ---

jar {
    baseName = 'moleculer-web'
    version =  '1.0.1'
}